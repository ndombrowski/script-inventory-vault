import os
import pandas as pd
import itertools

# Debugging statements
print("\nCurrent working directory:", os.getcwd())
#print("Contents of the directory:", os.listdir())

# path to Snakemakefile (used to identify the scripts folder in the smk files)
my_basedir = workflow.current_basedir
print("Snakemake directory:", my_basedir)

wildcard_constraints:
    sample = '[A-Za-z0-9-]+',
    output_dir = '[A-Za-z0-9-_]+'

# ======================================================
# Read in config file
# ======================================================
samples_table = pd.read_table(config["samples_file"], sep="\t", dtype = str).set_index("sample_id", drop=False)

# Define all possible combinations of samples, ignoring self-comparisons
sample_paths = samples_table['path'].to_dict()
sample_ids = samples_table['sample_id'].tolist()

pairs = [tuple(sorted(p)) for p in itertools.combinations(sample_ids, 2)]
pairs = sorted(set(pairs))
print("Sample pairs to be analysed:", pairs)

output_dir = config["output_folder"]
print("Results will be saved in:", output_dir)
if not os.path.exists(output_dir):
    os.makedirs(output_dir)
    

# ======================================================
# Functions
# ======================================================    
def get_fasta_paths(wildcards):
    sample1, sample2 = sorted([wildcards.sample1, wildcards.sample2])
    return {
        "fa1": sample_paths[sample1],
        "fa2": sample_paths[sample2],
    }
    
# ======================================================
# Rules
# ======================================================    
rule all:
    input:
        expand(
            os.path.join(output_dir, "dotplot/{taxon1}-{sample1}_vs_{taxon2}-{sample2}_dotplot.png"),
            zip,
            sample1=[a for a, b in pairs],
            sample2=[b for a, b in pairs],
            taxon1=[samples_table.loc[a, "taxon"] for a, b in pairs],  # Get taxon for sample1
            taxon2=[samples_table.loc[b, "taxon"] for a, b in pairs],  # Get taxon for sample2
        ),
        expand(
            os.path.join(output_dir, "dotplot/{taxon1}-{sample1}_vs_{taxon2}-{sample2}_dotplot.html"),
            zip,
            sample1=[a for a, b in pairs],
            sample2=[b for a, b in pairs],
            taxon1=[samples_table.loc[a, "taxon"] for a, b in pairs],  # Get taxon for sample1
            taxon2=[samples_table.loc[b, "taxon"] for a, b in pairs],  # Get taxon for sample2
        )


rule get_script:
    output:
        "scripts/pafCoordsDotPlotly.R"
    shell:
        """
        mkdir -p scripts
        wget -O {output} https://raw.githubusercontent.com/tpoorten/dotPlotly/refs/heads/master/pafCoordsDotPlotly.R
        """

rule run_minimap2:
    input:
        fa1 = lambda wildcards: get_fasta_paths(wildcards)["fa1"],
        fa2 = lambda wildcards: get_fasta_paths(wildcards)["fa2"]
    output:
        paf = os.path.join(output_dir, "minimap2/{sample1}_vs_{sample2}.paf")
    params:
        asm_preset = config["minimap2"]["asm-preset"]
    threads: 8
    conda: "env.yaml"
    shell:
        """
        minimap2 -x {params.asm_preset} -c -t {threads} {input.fa1} {input.fa2} > {output.paf}
        """

rule plot_dotplot:
    input:
        paf = lambda wildcards: os.path.join(output_dir, f"minimap2/{wildcards.sample1}_vs_{wildcards.sample2}.paf"),
        script = "scripts/pafCoordsDotPlotly.R"
    output:
        png = os.path.join(output_dir, "dotplot/{taxon1}-{sample1}_vs_{taxon2}-{sample2}_dotplot.png"),
        html = os.path.join(output_dir, "dotplot/{taxon1}-{sample1}_vs_{taxon2}-{sample2}_dotplot.html")
    conda: "env.yaml"
    params:
        m = config["pafCoordsDotPlotly"]["min-alignment-length"],
        q = config["pafCoordsDotPlotly"]["min-query-length"],
        taxon1 = lambda wildcards: samples_table.loc[wildcards.sample1, "taxon"],
        taxon2 = lambda wildcards: samples_table.loc[wildcards.sample2, "taxon"],
        basename = lambda wildcards: f"{samples_table.loc[wildcards.sample1, 'taxon']}-{wildcards.sample1}_vs_{samples_table.loc[wildcards.sample2, 'taxon']}-{wildcards.sample2}_dotplot"
    shell:
        """
        mkdir -p {output_dir}/dotplot

        Rscript {input.script} -i {input.paf} -o {params.basename} -m {params.m} -q {params.q} -l

        mv {params.basename}.png {output.png}
        mv {params.basename}.html {output.html}
        """

